[out:csv(::id, name, highway, service, surface, leisure, ::type)]  [timeout:425];
area(@AREA@)->.searchArea;
// all but service, footway, path + avoid bridges and special highways
way(area.searchArea)[highway][name][!"bridge"]["highway"!~"platform|bus_stop|footway|steps|raceway|service|pedestrian|proposed|abandoned|rest_area|path"];
out;
// service roads with names, avoid parking ways, drive-through
way(area.searchArea)[highway=service][name][!"bridge"][service!~"parking_aisle|drive-through"];
out;
// avoid underpass and bridge footways
way(area.searchArea)["highway"~"footway|steps|pedestrian"][name][layer!~"-"][!"bridge"];
out;
// path with valid street name, try to exclude hiking path with comments in name tag
way(area.searchArea)["highway"="path"][name][name~"utca|út|köz|árok|lépcső|gát|part|tér|sétány|park|dűlő|sor|kert"][name!~"garázssor"](area.searchArea);
out;
// include parks with names
way(area.searchArea)["leisure"="park"][name];
out;
relation(area.searchArea)["leisure"="park"][name];
out;
// area boundary streets
rel(@RELATION@)->.searchRelation;
way(r.searchRelation)[highway][name][!"bridge"];
out;
// highway=track can also be anonymous, but many mistag based on road surface
